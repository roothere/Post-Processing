#pragma kernel CSMain

#define PI 3.14159265358979323846f

Texture2D<float4> _SobelTex;
RWTexture2D<float4> _Result;


groupshared int edgeCount[64];

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID, uint3 gid : SV_GROUPTHREADID) {
    edgeCount[gid.x + gid.y * 8] = 0;

    int grid = (gid.y == 0) + (gid.x == 0);

    float3 sobel = _SobelTex[id.xy].xyz;

    float theta = sobel.y;

    float absTheta = abs(theta) / PI;
    
    int direction = -1;
    if (any(sobel.z)) {
        if ((0.0f <= absTheta) && (absTheta < 0.05f)) direction = 0; // VERTICAL
        else if ((0.9f < absTheta) && (absTheta <= 1.0f)) direction = 0;
        else if ((0.45f < absTheta) && (absTheta < 0.55f)) direction = 1; // HORIZONTAL
        else if (0.05f < absTheta && absTheta < 0.45f) direction = sign(theta) > 0 ? 2 : 3; // DIAGONAL 1
        else if (0.55f < absTheta && absTheta < 0.9f) direction = sign(theta) > 0 ? 3 : 2; // DIAGONAL 2
    }

    edgeCount[gid.x + gid.y * 8] = direction;

    GroupMemoryBarrierWithGroupSync();

    int commonEdgeIndex = -1;
    if ((gid.x == 0) && (gid.y == 0)) {
        uint buckets[4] = {0, 0, 0, 0};

        for (int i = 0; i < 64; ++i) {
            buckets[edgeCount[i]] += 1;
        }

        uint maxValue = 0;
        for (int j = 0; j < 4; ++j) {
            if (buckets[j] > maxValue) {
                commonEdgeIndex = j;
                maxValue = buckets[j];
            }
        }

        edgeCount[0] = commonEdgeIndex;
    }

    GroupMemoryBarrierWithGroupSync();

    commonEdgeIndex = edgeCount[0];

    float3 debugEdge = 0;
    if (commonEdgeIndex == 0) debugEdge = float3(1, 0, 0);
    if (commonEdgeIndex == 1) debugEdge = float3(0, 1, 0);
    if (commonEdgeIndex == 2) debugEdge = float3(0, 1, 1);
    if (commonEdgeIndex == 3) debugEdge = float3(1, 1, 0);

    float quantizedEdge = (commonEdgeIndex + 1) / 5.0f;

    // _Result[id.xy] = float4(debugEdge, 1.0f) + grid * 0.1f;
    _Result[id.xy] = quantizedEdge;
}